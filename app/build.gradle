apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.openapi.generator'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {

    signingConfigs {
        AppSigning {
            def Properties signingProps = new Properties()
            signingProps.load(new FileInputStream(file('../app/signing.properties')))
            storeFile file(signingProps["store"])
            storePassword signingProps["storePass"]
            keyAlias signingProps["alias"]
            keyPassword signingProps["keypass"]
            v1SigningEnabled true
            v2SigningEnabled true
        }

        ProdAppSigning {
            def Properties signingProps = new Properties()
            signingProps.load(new FileInputStream(file('../app/prodsigning.properties')))
            storeFile file(signingProps["store"])
            storePassword signingProps["storePass"]
            keyAlias signingProps["alias"]
            keyPassword signingProps["keypass"]
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdkVersion 30
    defaultConfig {
        applicationId "bg.dabulgaria.tibroish"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 10
        versionName "1.1.0.$versionCode"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        signingConfig signingConfigs.AppSigning
        setProperty("archivesBaseName", "Ti-Broish-$versionName")
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.ProdAppSigning
        }
        debug{
            debuggable true
            minifyEnabled false
            signingConfig = signingConfigs.AppSigning
        }
    }
    buildToolsVersion '29.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "free"
    productFlavors {

        def productionSigning = signingConfigs.ProdAppSigning
        def stagingSigning = signingConfigs.AppSigning

        production{

            applicationId = "bg.dabulgaria.tibroish"
            resValue "string", "provider_authority", "${applicationId}.file_provider_camera"
            def APP_NAME = "Ти броиш"
            resValue "string", "app_name", APP_NAME
            signingConfig productionSigning
        }
        staging{

            applicationId = "bg.dabulgaria.tibroish.apn"
            resValue "string", "provider_authority", "${applicationId}.file_provider_camera"
            def APP_NAME = "Ти броиш Stage"
            resValue "string", "app_name", APP_NAME
            signingConfig stagingSigning
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation "com.google.android.material:material:1.3.0"
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation platform('com.google.firebase:firebase-bom:27.1.0')

    implementation 'androidx.navigation:navigation-ui:2.3.5'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit2Version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.5.0'

    implementation "com.squareup.moshi:moshi:1.11.0"
    implementation "com.squareup.moshi:moshi-adapters:1.11.0"
    implementation "com.squareup.okhttp3:okhttp:4.5.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.11.0"

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation 'io.reactivex.rxjava3:rxjava:3.0.6'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.airbnb.android:lottie:3.4.0'

    // Dagger core
    api "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    // Dagger Android
    api "com.google.dagger:dagger-android:$daggerVersion"
    api "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    // Volley
    api 'com.android.volley:volley:1.2.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Test
    testImplementation 'junit:junit:4.13'
    //testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.4'
    // Truth
    testImplementation "com.google.truth:truth:1.1.3"
    // Android Test
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.github.pedroSG94.rtmp-rtsp-stream-client-java:rtplibrary:1.9.9'

}

repositories {
    jcenter()
    mavenCentral()
}

openApiGenerate {
    generatorName = "kotlin"
    inputSpec = "$rootDir/app/docs-json.json".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "bg.dabulgaria.tibroish.persistence.api"
    invokerPackage = "bg.dabulgaria.tibroish.persistence.repo"
    modelPackage = "bg.dabulgaria.tibroish.persistence.remote.model"
    validateSpec = false
    configOptions = [
            dateLibrary: "java8"
    ]
}
